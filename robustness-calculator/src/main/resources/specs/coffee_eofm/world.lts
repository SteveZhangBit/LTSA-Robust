const Absent = 0
const Empty = 1
const Full = 2
range TMugState = Absent..Full

const False = 0
const True = 1
range TBool = False..True

const EmptyOrUsed = 0
const New = 1
range TPodState = EmptyOrUsed..New

WORLD = VAR[False][Absent][True][EmptyOrUsed],
VAR[iBrewing:TBool][iMugState:TMugState][iHandleDown:TBool][iPodState:TPodState] = (
      when (iMugState == Absent) hPlaceMug -> VAR[iBrewing][Empty][iHandleDown][iPodState]
    | when (iMugState != Absent) hTakeMug -> VAR[iBrewing][Absent][iHandleDown][iPodState]
    | when (iHandleDown == True) hLiftHandle -> VAR[iBrewing][iMugState][False][iPodState]
    | when (iHandleDown == False) hLowerHandle -> VAR[iBrewing][iMugState][True][iPodState]
    | when (1) hAddOrReplacePod -> VAR[iBrewing][iMugState][iHandleDown][New]
    | when (iPodState == New) hPressBrew -> VAR[True][iMugState][iHandleDown][EmptyOrUsed]
    | when (iPodState != New) hPressBrew -> VAR[True][iMugState][iHandleDown][iPodState]
    | when (iBrewing == True && iMugState == Empty) mBrewDone -> VAR[False][Full][iHandleDown][iPodState]
    | when (iBrewing == True && iMugState == Absent) mBrewDone -> VAR[False][iMugState][iHandleDown][iPodState]
).
