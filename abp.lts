range BIT = 0 .. 1

TRANSMITTER = ACCEPT[0],
ACCEPT[b:BIT] = (accept -> SEND[b]),
SEND[b:BIT] = (send[b] -> SENDING[b]),
SENDING[b:BIT] = (txto -> SEND[b]
                | ack[b] -> ACCEPT[!b]
                | ack[!b] -> SENDING[b]).

RECEIVER = REPLY[1],
DELIVER[b:BIT] = (deliver -> REPLY[b]),
REPLY[b:BIT] = (reply[b] -> REPLYING[b]),
REPLYING[b:BIT] = (rxto -> REPLY[b]
                 | rec[!b] -> DELIVER[!b]
                 | rec[b] -> REPLYING[b]).

//CHANNEL = ( in[b:BIT] -> lose -> Channel
//          | in[b:BIT] -> out[b] -> Channel)
//          @ {in, out}.

CHANNEL = (in[b:BIT] -> LOSE[b]
         | in[b:BIT] -> TRANSMIT[b]),
LOSE[b:BIT] = (lose -> CHANNEL
             | in[i:BIT] -> LOSE[i]
             | in[i:BIT] -> TRANSMIT[i]),
TRANSMIT[b:BIT] = (out[b] -> CHANNEL
                 | in[i:BIT] -> LOSE[i]
                 | in[i:BIT] -> TRANSMIT[i])
                 @{in, out}.

||ABP = (trans:CHANNEL || tx:TRANSMITTER || rx:RECEIVER || ack:CHANNEL)
      / {deliver/rx.deliver, accept/tx.accept, trans.in/tx.send,
         rx.rec/trans.out, ack.in/rx.reply, ack.out/tx.ack}
      @ {accept, deliver}.

property AD = (accept -> deliver -> AD).
